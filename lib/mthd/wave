%******************************************
%*
%*      The WAVE methods
%*
%******************************************


        % WAVE (TRANSVERSE) METHOD: 
        % Find a subexpression to which a 
        % (conditional) wave-rule applies,
        % where we require the condition 
        % of the rule to hold already. In
        % addition the rewritten wave-front 
        % must be positioned over a sink.
method(wave(Pos,[Rule,Dir]),
       H==>G,
       [wave_occ_at(G,Pos,L),
        wave_rule_match(Rule,trans(Dir),C=>L:=>R,SubstL),
        trivial(H==>C),
        sinkable(R)   
       ],
       [replace_in_matrix(Pos,R,G,NG),
        sink_expansions(NG,NewG)],
       [H==>NewG],
       SubstL
      ).

        % WAVE (LONGITUDINAL) METHOD: 
        % Find a subexpression to which a 
        % (conditional) wave-rule applies,
        % where we require the condition 
        % of the rule to hold already.
method(wave(Pos,[Rule,Dir]),
       H==>G,
       [wave_occ_at(G,Pos,L),
        wave_rule_match(Rule,long(Dir),C=>L:=>R,SubstL),
        trivial(H==>C)
       ],
       [replace_in_matrix(Pos,R,G,NG),
        sink_expansions(NG,NewG)],
       [H==>NewG],
       SubstL
      ).





